apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.javalive09.demos"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //防止上传到jcenter lint error 检查
    lintOptions {
        abortOnError false
    }
}

repositories {
    flatDir {
        dirs 'libs'   // aar目录
    }

}

dependencies {
    implementation 'com.javalive09.codebag:codebag:1.6.1'
    implementation 'com.javalive09.annotation:annotation:1.0.5'
    annotationProcessor 'com.javalive09.processor:processor:1.6'

//        implementation project(':library')

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'cn.aigestudio.wheelpicker:WheelPicker:1.1.2'

    implementation 'com.github.promeg:tinypinyin:2.0.3'
    implementation 'com.github.lzyzsd:jsbridge:1.0.4'
    implementation 'org.jooq:joor:0.9.6'

//    implementation(name: 'lib-debug', ext: 'aar')
    implementation 'com.javalive09.rxipc:rxipc:1.0.5'

}

//>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>添加长按查看源码功能
afterEvaluate {
    android.applicationVariants.all { variant ->
        variant.assemble.dependsOn(generateSources)
    }
}
task generateSources doLast {

    //加载资源
    Properties properties = new Properties()
    InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
    properties.load(inputStream)
    //读取字段
    def show_source = properties.getProperty('show_source')

    if (show_source == 'true') {
        def src = android.sourceSets.findByName("main").java.srcDirs.find().toString()
        def dest = android.sourceSets.findByName("main").assets.srcDirs.find().toString()
        println 'src >>>>>>>>> ' + src.toString()
        println 'dest >>>>>>>> ' + dest.toString()
        delete fileTree(dest)
        copy {
            from(src)
            into(dest)
            rename { String fileName ->
                fileName.replace(".java", ".source")
            }
        }
    } else {
        println 'show_source >>>>>>>> ' + show_source
    }
}
